<HTML>
<HEAD>
<TITLE>RTC Client VBScript Test</TITLE>
</HEAD>

<P><FONT size=6>RTC Client VBScript Test</FONT></P>

<P><FONT size=4><STRONG>User URI:&nbsp;</STRONG></FONT>
<INPUT id=UserURIField name=text1 style="HEIGHT: 28px; WIDTH: 190px"></P>

<P><FONT size=4><STRONG>Session State:&nbsp;</STRONG></FONT>
<INPUT id=SessionStateField name=text2 style="HEIGHT: 28px; WIDTH: 190px"></P>
<P>&nbsp;</P>

<P><INPUT id=CallButton name=button1 style="HEIGHT: 36px; LEFT: 132px; POSITION: absolute; TOP: 230px; WIDTH: 134px; Z-INDEX: 100" type=button value=Call></P>

<P><INPUT id=DisconnectButton name=button2 style="HEIGHT: 36px; LEFT: 132px; POSITION: absolute; TOP: 280px; WIDTH: 134px; Z-INDEX: 101" type=button value=Disconnect></P>

<P><INPUT id=ShutdownButton name=button3 style="HEIGHT: 36px; LEFT: 132px; POSITION: absolute; TOP: 330px; WIDTH: 134px; Z-INDEX: 102" type=button value=Shutdown></P>

<P>
<OBJECT classid=CLSID:7a42ea29-a2b7-40c4-b091-f6f024aa89be id=Client></OBJECT>
</P>

<SCRIPT language=VBScript>

'=================================================================================
' Some RTC Constants
'

' ----------------------------
' RTC EVENTS
' ----------------------------
Const RTCE_CLIENT                    = 0
Const RTCE_REGISTRATION_STATE_CHANGE = 1
Const RTCE_SESSION_STATE_CHANGE      = 2
Const RTCE_SESSION_OPERATION_COMPLETE= 3
Const RTCE_PARTICIPANT_STATE_CHANGE  = 4
Const RTCE_MEDIA                     = 5
Const RTCE_INTENSITY                 = 6
Const RTCE_MESSAGING                 = 7
Const RTCE_BUDDY                     = 8
Const RTCE_WATCHER                   = 9

Const RTCEF_CLIENT                    = 1
Const RTCEF_REGISTRATION_STATE_CHANGE = 2
Const RTCEF_SESSION_STATE_CHANGE      = 4
Const RTCEF_SESSION_OPERATION_COMPLETE= 8
Const RTCEF_PARTICIPANT_STATE_CHANGE  = 16
Const RTCEF_MEDIA                     = 32
Const RTCEF_INTENSITY                 = 64
Const RTCEF_MESSAGING                 = 128
Const RTCEF_BUDDY                     = 256
Const RTCEF_WATCHER                   = 512

'-----------------------------
' RTC_SESSION_STATE
'-----------------------------
Const RTCSS_IDLE         = 0
Const RTCSS_INCOMING     = 1
Const RTCSS_ANSWERING    = 2
Const RTCSS_INPROGRESS   = 3
Const RTCSS_CONNECTED    = 4
Const RTCSS_DISCONNECTED = 5

'-----------------------------
' RTC_SESSION_TYPE
'-----------------------------
Const RTCST_PC_TO_PC       = 0
Const RTCST_PC_TO_PHONE    = 1
Const RTCST_PHONE_TO_PHONE = 2
Const RTCST_IM             = 3

'-----------------------------
' RTC_LISTEN_MODE
'-----------------------------
Const RTCLM_NONE           = 0
Const RTCLM_DYNAMIC        = 1
Const RTCLM_BOTH           = 2

'-----------------------------
' RTC_TERMINATE_REASON
'-----------------------------
Const RTCTR_NORMAL         = 0
Const RTCTR_DND            = 1
Const RTCTR_BUSY           = 2
Const RTCTR_REJECT         = 3
Const RTCTR_TIMEOUT        = 4
Const RTCTR_SHUTDOWN       = 5

'-----------------------------
' RTC_VIDEO_DEVICE
'-----------------------------
Const RTCVD_RECEIVE        = 0
Const RTCVD_PREVIEW        = 1

'-----------------------------
' RTC_MEDIA_EVENT_TYPE
'-----------------------------
Const RTCMET_STOPPED       = 0
Const RTCMET_STARTED       = 1
Const RTCMET_PAUSED        = 2
Const RTCMET_RESUMED       = 3

'-----------------------------
' RTC_MEDIA_TYPES
'-----------------------------
Const RTCMT_AUDIO_SEND     = 1
Const RTCMT_AUDIO_RECEIVE  = 2
Const RTCMT_VIDEO_SEND     = 4
Const RTCMT_VIDEO_RECEIVE  = 8
Const RTCMT_T120_SENDRECV  = 16

'=================================================================================
' Globals
'

Dim objSession


'=================================================================================
' SessionState
'
Function SessionState(state, session)


    If state = RTCSS_IDLE Then
        SessionStateField.Value = "IDLE"
    ElseIf state = RTCSS_INCOMING Then
        SessionStateField.Value = "INCOMING"

	Client.PlayRing 0, TRUE

	Set objSession = session

	objSession.Answer

    ElseIf state = RTCSS_ANSWERING Then
        SessionStateField.Value = "ANSWERING"
    ElseIf state = RTCSS_INPROGRESS Then
        SessionStateField.Value = "INPROGRESS"
    ElseIf state = RTCSS_CONNECTED Then
        SessionStateField.Value = "CONNECTED"
    ElseIf state = RTCSS_DISCONNECTED Then
        SessionStateField.Value = "DISCONNECTED"
    End If

End Function
  
'=================================================================================
' Media
'
Function Media(mediatype, eventtype)

    If eventtype = RTCMET_STARTED Then
        If mediatype = RTCMT_VIDEO_SEND Then
            Client.VideoWindowVisible(RTCVD_PREVIEW) = TRUE
        ElseIf mediatype = RTCMT_VIDEO_RECEIVE Then
            Client.VideoWindowVisible(RTCVD_RECEIVE) = TRUE
        End If
    End If
        
End Function 

'=================================================================================
' RTCEvent Handler
' 
' handles events fired by RTC
' 

Sub Client_Event(RtcEvent, pEvent)

    If RtcEvent = RTCE_SESSION_STATE_CHANGE Then
        Call SessionState(pEvent.State, pEvent.Session)
    ElseIf RtcEvent = RTCE_MEDIA Then
        Call Media(pEvent.MediaType, pEvent.EventType)
    End If

End Sub


'=================================================================================
' CallButton_onClick
'

Sub CallButton_onClick()

    Set objSession = Client.CreateSession (RTCST_PC_TO_PC,"",Nothing,0)

    objSession.AddParticipant UserURIField.Value, "" 

End Sub

'=================================================================================
' DisconnectButton_onClick
'

Sub DisconnectButton_onClick()

    objSession.Terminate RTCTR_NORMAL 

End Sub

'=================================================================================
' ShutdownButton_onClick
'

Sub ShutdownButton_onClick()

    SessionStateField.Value = ""
    UserURIField.Value = ""

    Client.Shutdown

End Sub

'=================================================================================

Client.Initialize

Client.EventFilter = RTCEF_SESSION_STATE_CHANGE + RTCEF_MEDIA

Client.ListenForIncomingSessions = RTCLM_BOTH

</SCRIPT>

</BODY>
</HTML>
